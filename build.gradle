plugins {
    id 'io.jmix' version '0.1.11'
    id 'java'
    id 'groovy'
}

apply plugin: 'org.springframework.boot'

jmix {
    bomVersion = '0.1.0-SNAPSHOT'
}

group = 'com.company'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'https://nexus.jmix.io/repository/public'
    }
}

dependencies {
    implementation 'io.jmix.core:jmix-core-starter'
    implementation 'io.jmix.data:jmix-data-starter'
    implementation 'io.jmix.ui:jmix-ui-starter'
    implementation 'io.jmix.ui:jmix-ui-data-starter'
    implementation 'io.jmix.ui:jmix-ui-themes-compiled'
    implementation 'io.jmix.ui:jmix-ui-widgets-compiled'
    implementation 'io.jmix.security:jmix-security-starter'
    implementation 'io.jmix.security:jmix-security-ui-starter'
    implementation 'io.jmix.security:jmix-security-data-starter'

    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation('org.jsoup:jsoup') {
        version {
            strictly '1.11.2'
        }
    }

    runtimeOnly 'org.hsqldb:hsqldb'

    testImplementation 'io.jmix.masquerade:jmix-masquerade:1.0-SNAPSHOT'
    testImplementation 'com.codeborne:selenide:5.14.0'
    testImplementation 'org.slf4j:slf4j-simple:1.7.30'
    testImplementation 'org.codehaus.groovy:groovy:3.0.5'
    testImplementation 'org.codehaus.groovy:groovy-sql:3.0.5'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.apache.commons:commons-collections4:4.4'

    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

task testUi(type: Test) {

    useJUnitPlatform()

    systemProperty 'selenide.savePageSource', 'false'

    if (System.getProperty('webdriver.chrome.driver')) {
        systemProperty 'webdriver.chrome.driver', System.getProperty('webdriver.chrome.driver')
    }

    def javaProperties = System.getProperties()
    for (def entry in javaProperties.entrySet()) {
        if (String.valueOf(entry.key).startsWith('selenide.')) {
            systemProperty(String.valueOf(entry.key), entry.value)
        }
    }

    filter {
        includeTestsMatching "io.jmix.tests.ui.*"
    }

    if (System.getProperty('jmix.tests.ui.baseUrl')) {
        systemProperty 'selenide.baseUrl', System.getProperty('jmix.tests.ui.baseUrl')
    } else {
        systemProperty 'selenide.baseUrl', 'http://localhost:8080/ui-tests'
    }
}

task testSampler(type: Test) {
    useJUnitPlatform()

    systemProperty 'selenide.savePageSource', 'false'

    if (System.getProperty('webdriver.chrome.driver')) {
        systemProperty 'webdriver.chrome.driver', System.getProperty('webdriver.chrome.driver')
    }

    def javaProperties = System.getProperties()
    for (def entry in javaProperties.entrySet()) {
        if (String.valueOf(entry.key).startsWith('selenide.')) {
            systemProperty(String.valueOf(entry.key), entry.value)
        }
    }

    filter {
        includeTestsMatching "io.jmix.tests.sampler.*"
    }

    if (System.getProperty('jmix.tests.sampler.baseUrl')) {
        systemProperty 'selenide.baseUrl', System.getProperty('jmix.tests.sampler.baseUrl')
    } else {
        systemProperty 'selenide.baseUrl', 'http://localhost:8080/sampler'
    }
}

test.dependsOn testUi, testSampler
